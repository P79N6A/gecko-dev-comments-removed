// Configure shared Android settings.  This should be run (using "apply from")
// immediately after applying the Android plugin.  Be aware that IntelliJ does
// not parse these values correctly: the Android-Gradle facet panel manually
// parses build.gradle rather than interrogating the Gradle model.

android {
    compileSdkVersion "android-${mozconfig.defines.ANDROID_TARGET_SDK}"
    // Turn "android-sdk/build-tools/21.1.1" into "21.1.1".
    buildToolsVersion (new File(mozconfig.substs.ANDROID_BUILD_TOOLS).getName())

    defaultConfig {
        targetSdkVersion mozconfig.defines.ANDROID_TARGET_SDK
        minSdkVersion mozconfig.defines.MOZ_ANDROID_MIN_SDK_VERSION
        if (mozconfig.defines.MOZ_ANDROID_MAX_SDK_VERSION) {
            maxSdkVersion mozconfig.defines.MOZ_ANDROID_MAX_SDK_VERSION
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    android {
        lintOptions {
            abortOnError false
        }
    }
}
